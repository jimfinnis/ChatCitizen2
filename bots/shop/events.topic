:RIGHTCLICK |:t|
    hourscheck if stop then
    ?itemheld ?@exchange = if

        # we are holding the exchange item (i.e. my money type)
        
        ?buyingItem isnone not if
            ?@thingsisell isnone if
                "You're buying something but I don't sell. How odd.". stop
            then

            # we are buying something (set in conversation in attemptsetbuy)
            # so try to take that number of exchange items

            ?buyingItem ?@thingsisell get ?@exchange take !t
            
            # print some status
            cases
                ?t "UNKNOWN" = if "I don't know what that is.". case
                ?t "NOITEM" = if ^noitem. case
                ?t "WRONG" = if ^wrong. case
                ?t "NOTENOUGH" = if ^notenough. case
                ?t "OK" = if 

                    # if it worked, print thanks and give the player N of the
                    # item they are after.
                    
                    ?buyingItem ?@thingsisellextra get !t
                    ?t isnone if 1 !t then
                    ^thanks.
                    ?t ?buyingItem give drop # drop the results of the give command
                case
                # print this if some weird code was returned from the take.
                "Well, that's weird. Did you feel that?".
                ?t.
             otherwise
        else
            # we are holding the exchange item but we don't want to buy anything!
            # This will trigger a conversational pattern.
            "Nice of you, but what are you buying?". buywhat
        then   
    else

        # we aren't holding the exchange item    

        ?itemheld "NOITEM" = if
            # we're holding nothing, that's cheeky!
            ^noitem.
        else
            # we are holding something - is it something we buy?
            ?@thingsibuy isnone if
                ["I'm sorry, I don't buy anything.",
                "I don't buy anything.",
                "I don't buy anything, " honor +
               ] choose. stop
            then
            ?itemheld ?@thingsibuy get !t
            ?t isnone if
                # it isn't (we don't have a value for it in ?@thingsibuy)
                ^wrong.
            else
                # it is! Try to take the appropriate number.
                ?t ?itemheld take !t
                cases
                    ?t "UNKNOWN" = if "That's a very odd object.". case
                    ?t "NOITEM" = if "A handful of air...". case
                    ?t "WRONG" = if "The wrong item.". case
                    ?t "NOTENOUGH" = if ^notenough. case
                    ?t "OK" = if
                        # OK, that worked - we have taken N of that item.
                        # By default we give 1 exchange item in return, but
                        # that can be modified.
                        ?itemheld ?@thingsibuyextra get !t
                        ?t isnone if 1 !t then
                        # give that many exchange items
                        ^thanks.
                        ?t ?@exchange give drop
                    case
                # in case the take returned something weird.
                "Oooh, shivery.". otherwise
            then
        then
    then
    out clean      
;

:pleasestop
    ["Ow!","Hey!","Ouch!","Stop that!","What was that for?"] choose;

:ENTITYHITME pleasestop;
:PLAYERHITME pleasestop;
